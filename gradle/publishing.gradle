apply plugin: "io.codearte.nexus-staging"

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        repositories {
            maven {
                url = snapshot
                        ? "https://oss.sonatype.org/content/repositories/snapshots"
                        : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                name = "Sonatype"

                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                artifactId = "${artifactName}-${project.name}"
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = projectName
                    description = projectDescription
                    inceptionYear = '2018'
                    url = "https://github.com/xdrop/fuzzywuzzy"

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = "xdrop"
                            name = "Panayiotis P"
                            url = "http://www.github.com/xdrop"
                            timezone = "0"
                        }
                    }

                    contributors {
                        contributor {
                            id = "burdoto"
                            name = "Tobias Burdow"
                            url = "http://www.github.com/burdoto"
                            timezone = "+2"
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/xdrop/fuzzywuzzy.git'
                        developerConnection = 'scm:git:ssh://github.com/xdrop/fuzzywuzzy.git'
                        url = 'https://github.com/xdrop/fuzzywuzzy'
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/xdrop/fuzzywuzzy/issues'
                    }
                }
            }
        }
    }

    task generateGithubPom(type: GenerateMavenPom) {
        destination = file("$projectDir/pom.xml")
        pom = publishing.publications.mavenJava.pom
    }

    if (!System.getenv().containsKey('JITPACK')) {
        signing {
            useGpgCmd()
            sign publishing.publications.mavenJava
        }
    }
}

tasks.removeAll([promoteRepository, closeAndPromoteRepository, getStagingProfile])

nexusStaging {
    username sonatypeUsername
    password sonatypePassword
}

